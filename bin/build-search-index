#!/usr/bin/perl -Ilib/ -I../lib/

=head1 NAME

build-search-index - Build a search index for Lucy.

=head1 DESCRIPTION

This script is designed to build a search index of all available
articles to a temporary directory beneath /tmp.

The search is queried by L<Lucy::Simple>, for providing visitors with
a search facility.

=cut

=head1 AUTHOR

 Steve
 --
 http://www.steve.org.uk/

=cut

=head1 LICENSE

Copyright (c) 2014 by Steve Kemp.  All rights reserved.

This script is free software; you can redistribute it and/or modify it under
the same terms as Perl itself.

The LICENSE file contains the full text of the license.

=cut

use strict;
use warnings;

use File::Path;
use Singleton::DBI;
use Lucy::Simple;


#
# Ensure the index directory is both available and empty.
#
my $index = "/tmp/index";

if ( -d $index )
{
    system( "rm", "-rf", $index );
}

File::Path::make_path( '/tmp/index', { verbose => 0, mode => 0755 } );



#
#  Create the helper.
#
my $lucy = Lucy::Simple->new( path => $index, language => 'en', );


#
# Connect to the database
#
my $dbi = Singleton::DBI->instance() or
  die "Unable to connect to database";

#
# Select the data
#
my $sql =
  $dbi->prepare("select title,bodytext,id FROM articles ORDER by id ASC");
$sql->execute();

#
# Bind & Fetch results
#
my ( $title, $content, $id );
$sql->bind_columns( undef, \$title, \$content, \$id );
while ( $sql->fetch() )
{
    my %data = ( title => $title, body => $content, id => $id );
    print "$id - $title\n";
    $lucy->add_doc( \%data );
}
$sql->finish();
$dbi->disconnect();
